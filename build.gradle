buildscript {
    ext.kotlinVersion = '0.13.1514'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: 'jacoco'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
	jcenter()
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile 'com.beust:klaxon:0.15'

    testCompile 'org.testng:testng:6.9.4'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

test {
    useTestNG()
}

//Workaround for 'Cannot configure the 'publishing' extension after it has been accessed.' error
Properties publishProperties = new Properties()
publishProperties.load(file('publish.properties').newInputStream())

def publish = [
        group: publishProperties.get('publish.group'),
        name: publishProperties.get('publish.name'),
        version: publishProperties.get('publish.version'),
]

publishing {
    publications {
        JCenterPublication(MavenPublication) {
            from components.java

            groupId = publish.group
            artifactId = publish.name
            version = publish.version
        }
    }
}

bintray {
    user = project.properties.get('jcenter.personal.user')
    key = project.properties.get('jcenter.personal.api_key')
    publications = ['JCenterPublication']
    pkg {
        repo = 'maven'
        group = publish.group
        name = publish.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/fboldog/ext4klaxon.git'
        labels = ['kotlin']
        version {
            name = publish.version
            desc = 'Type Extensions for Klaxon'
            released = new Date()
            vcsTag = publish.version
        }
    }
}

task install(dependsOn: publishToMavenLocal)

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}