buildscript {
    ext.kotlinVersion = '1.0.0'
    ext.dokkaVersion = '0.9.7'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

repositories {
	jcenter()
//    maven {
//        url "https://jitpack.io"
//    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile 'com.beust:klaxon:0.24'
    testCompile 'org.testng:testng:6.9.10'
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

test {
    useTestNG()
}

//Workaround for 'Cannot configure the 'publishing' extension after it has been accessed.' error
Properties publishProperties = new Properties()
publishProperties.load(file('publish.properties').newInputStream())

def publish = [
        group: publishProperties.get('publish.group'),
        name: publishProperties.get('publish.name'),
        version: publishProperties.get('publish.version'),
]

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}


publishing {
    publications {
        JCenterPublication(MavenPublication) {
            from components.java
            artifact sourceJar

            groupId = publish.group
            artifactId = publish.name
            version = publish.version
        }
    }
}

bintray {
    user = project.properties.get('jcenter.personal.user')
    key = project.properties.get('jcenter.personal.api_key')
    publications = ['JCenterPublication']
    pkg {
        repo = 'maven'
        group = publish.group
        name = publish.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/fboldog/ext4klaxon.git'
        labels = ['kotlin']
        version {
            name = publish.version
            desc = 'Type Extensions for Klaxon'
            released = new Date()
            vcsTag = publish.version
        }
    }
}

dokka {
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/fboldog/ext4klaxon/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}

task install(dependsOn: publishToMavenLocal)

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
